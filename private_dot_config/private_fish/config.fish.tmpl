###################################
# Variables
###################################

# Sometimes this is not in PATH
set -ag fish_user_paths "$HOME"/.local/bin
set -ag fish_user_paths /lib/passenger/bin
set -ag fish_user_paths /usr/lib/passenger/bin
set -ag fish_user_paths $HOME/.cargo/bin
set -ag fish_user_paths $HOME/.dotnet
set -ag fish_user_paths $HOME/.yarn/bin
set -ag fish_user_paths /home/linuxbrew/.linuxbrew/bin
set -ag fish_user_paths $HOME/.linuxbrew/bin

set -gx EDITOR "nvim"

# Go
if type -q go
    if ! set -q GOPATH; set -Ux GOPATH "$HOME/go"; end
    if ! set -q GOBIN; set -Ux GOBIN "$GOPATH/bin"; end
    if ! set -q GO111MODULE; set -Ux GO111MODULE "on"; end
    set -ag fish_user_paths "$GOBIN"
end

# Colorize manpages using bat
if type -q bat
    if ! set -q MANPAGER; set -Ux MANPAGER 'sh -c "col -bx | bat --language=man --style=grid --color=always --decorations=always"'; end
    if ! set -q MANROFFOPT set -Ux MANROFFOPT '-c'; end
end

# fzf
if type -q fzf
    # margin, height and color
    set -gx FZF_DEFAULT_OPTS "--margin 0,1 --layout reverse --height 60% --color 'fg:#bbccdd,fg+:#ddeeff,bg:#334455,preview-bg:#223344,border:#778899'"

    # Default command
    if type -q rg
        set -gx _BASE_FZF_DEFAULT_COMMAND 'rg --files --hidden --follow -g "!**/{.git,node_modules}/**"'
        set -gx FZF_DEFAULT_COMMAND {$_BASE_FZF_DEFAULT_COMMAND}' $dir 2> /dev/null'
    else
        set -gx _BASE_FZF_DEFAULT_COMMAND 'find -L . \( -path ./.git -prune -path ./node_modules -prune \) -o -print -type f 2> /dev/null'
        set -gx FZF_DEFAULT_COMMAND "$_BASE_FZF_DEFAULT_COMMAND"
    end
    set -gx FZF_CTRL_T_COMMAND "$FZF_DEFAULT_COMMAND"

    # search for a file in the home directory
    if type -q fd
        set -gx FZF_ALT_C_COMMAND "fd -t d --full-path './'"
    else
        set -gx FZF_ALT_C_COMMAND "find . -type d"
    end


    # jethrokuan/fzf fish plugin
    set -gx FZF_CD_WITH_HIDDEN_COMMAND "fd -t d --hidden --full-path './'"
    set -gx FZF_PREVIEW_DIR_CMD "exa --tree --level 3"
    set -gx FZF_PREVIEW_FILE_CMD "bat --style=numbers --color=always"

    set -gx FZF_FIND_FILE_COMMAND {$_BASE_FZF_DEFAULT_COMMAND}' . \$dir 2> /dev/null'
    set -gx FZF_OPEN_COMMAND "$FZF_FIND_FILE_COMMAND"
    set -gx FZF_CD_COMMAND "$FZF_ALT_C_COMMAND"
    set -gx FZF_ENABLE_OPEN_PREVIEW 1
    if ! set -q FZF_COMPLETE; set -Ux FZF_COMPLETE 3; end

    # preview files on the right side
    set -gx FZF_CTRL_T_OPTS "--preview '"{$FZF_PREVIEW_FILE_CMD}' {}\''
    # preview directory sub tree
    set -gx FZF_ALT_C_OPTS "--preview '"{$FZF_PREVIEW_DIR_CMD}' {}\''


    # fzf-complete with ctrl+x
    bind \ex '__fzf_alt_x'

    alias cf 'fzf-bcd-widget'

    {{ if eq .chezmoi.osRelease.id "fedora" -}}
    source /usr/share/fzf/shell/key-bindings.fish
    {{ end -}}
end

###################################
# Auto add ssh keys at login
###################################
if status --is-login
    setenv SSH_ENV $HOME/.ssh/environment

    if [ -n "$SSH_AGENT_PID" ]
        ps -ef | grep $SSH_AGENT_PID | grep ssh-agent >/dev/null
        if [ $status -eq 0 ]
            add_identities
        end
    else
        if [ -f $SSH_ENV ]
            . $SSH_ENV >/dev/null
        end
        ps -ef | grep $SSH_AGENT_PID | grep -v grep | grep ssh-agent >/dev/null
        if [ $status -eq 0 ]
            add_identities
        else
            start_agent
        end
    end

end

###################################
# Auto add fisher and plugins
# on a fresh install
###################################
if not functions -q fisher
    set -q XDG_CONFIG_HOME; or set XDG_CONFIG_HOME $HOME/.config
    curl https://git.io/fisher --create-dirs -sLo $XDG_CONFIG_HOME/fish/functions/fisher.fish
    fish -c fisher
end

###################################
# Keybindings
###################################

bind \cH backward-kill-path-component

###################################
# Aliases
###################################

alias cl 'clear'

if type -q starship
    eval (starship init fish)
end

{{ if eq .chezmoi.osRelease.id "arch" "manjaro" -}}
source /opt/asdf-vm/asdf.fish
{{ else }}
source "$HOME"/.asdf/asdf.fish
{{ end -}}
